"""Run `kipoi veff score_variants` for multiple models and multiple vcf files
"""

import kipoi

# config - TODO update
vcf_sets = ['variants', 'clinvar_20180429.enhancer-regions.chr22']
fasta_file = 'input/hg38_chr22.fa'

assert os.path.exists(fasta_file)

models = ['DeepSEA/variantEffects', 'Basset', 'DeepBind/Homo_sapiens/TF/D00328.018_ChIP-seq_CTCF']

file_formats = ['vcf', 'tsv', 'h5']

rule all:
    input:
        expand("output/{model}/{vcf_set}.{ext}",
               vcf_set=vcf_sets,
               model=models,
               ext=file_formats)

# kipoi veff score_variants DeepSEA/score_a --dataloader_args='{"fasta_file": "inputs/hg38_chr22.fa"}' \
# -i input/variants.vcf.gz -o output/variants.vcf -e output/variants.tsv

rule score_variants:
    input:
        vcf = "input/{vcf_set}.vcf.gz",
        fasta_file = fasta_file
    output:
        f = "output/{model}/{vcf_set}.{ext}",
    params:
        output_flag = lambda wildcards: "-o" if wildcards.ext == "vcf" else "-e",
        workers = 10,  # number of workers
        batch_size = 32
    shell:
        """
        mkdir -p `dirname {output.f}`

        kipoi_cmd=$(kipoi env get_cli {wildcards.model})

        ${kipoi_cmd} veff score_variants \
            {wildcards.model} \
            --dataloader_args='{{"fasta_file": "{input.fasta_file}"}}' \
            -i {input.vcf} \
            -n {params.workers} \
            --batch_size={params.batch_size} \
            {params.output_flag} {output.f} \
            -s ref alt logit_ref logit_alt diff \
            --std_var_id
        """
# TODO - remove source=dir, gtf_file and preproc_transformer

# ------------------------------------------------


CLINVAR_DATE = "20180429"  # clinvar dowload date


rule download_clinvar:
    """Download and filter clinvar:
    - remove indels
    - keep only chromosome 22
    - keep only variants where alt != '.'
    - replace 22 -> chr22
    """
    input:
        filt_region = "../1-predict/input/enhancer-regions.hg19.chr22.bed.gz"
    output:
        vcf = "input/clinvar_{}.enhancer-regions.chr22.vcf.gz".format(CLINVAR_DATE)
    shell:
        """
        wget -O - ftp://ftp.ncbi.nlm.nih.gov/pub/clinvar/vcf_GRCh37/archive_2.0/2018/clinvar_{CLINVAR_DATE}.vcf.gz | \
          zcat | vcftools --vcf - --remove-indels --recode --recode-INFO-all -c --chr 22 | \
          awk '$5 != "."' | \
          sed  -e 's/^22/chr22/' | \
          bedtools intersect -a stdin -b {input.filt_region} -wa -u -header |
          gzip -c > {output.vcf}
        """
