"""Run `kipoi veff score_variants` for multiple models and multiple vcf files
"""
import kipoi

# --------------------------------------------
# Config
CLINVAR_DATE = "20180429"  # clinvar dowload date

vcf_sets = ['clinvar_{}.pathogenic.chr22'.format(CLINVAR_DATE)]
fasta_file = '../1-predict/input/hg19.chr22.fa'
assert os.path.exists(fasta_file)

# get all DeepBind models in trained on human ChIP-seq
df = kipoi.list_models()
deepbind_models = df.model[df.model.str.match("DeepBind/Homo_sapiens/TF/.*_ChIP-seq.*")].tolist()
assert len(deepbind_models) == 137

# which models to use
models = deepbind_models[:10] + ['DeepSEA/variantEffects', 'Basset']

# output file format
file_formats = ['tsv']  # additional: ['vcf', 'h5']
# --------------------------------------------

rule all:
    input:
        expand("output/{model}/{vcf_set}.{ext}",
               vcf_set=vcf_sets,
               model=models,
               ext=file_formats)

# kipoi veff score_variants DeepSEA/score_a --dataloader_args='{"fasta_file": "inputs/hg19.chr22.fa"}' \
# -i input/variants.vcf.gz -o output/variants.vcf -e output/variants.tsv

rule score_variants:
    input:
        vcf = "input/{vcf_set}.vcf.gz",
        fasta_file = fasta_file
    output:
        f = "output/{model}/{vcf_set}.{ext}",
    params:
        output_flag = lambda wildcards: "-o" if wildcards.ext == "vcf" else "-e",
        workers = 4,  # number of workers
        batch_size = 32,
        score = lambda wildcards: "diff" if wildcards.model.startswith("DeepBind") else "logit"
    shell:
        """
        mkdir -p `dirname {output.f}`

        kipoi_cmd=$(kipoi env get_cli {wildcards.model})

        $kipoi_cmd veff score_variants \
            {wildcards.model} \
            --dataloader_args='{{"fasta_file": "{input.fasta_file}"}}' \
            -i {input.vcf} \
            -n {params.workers} \
            --batch_size={params.batch_size} \
            {params.output_flag} {output.f} \
            -s {params.score} \
            --std_var_id
        """


rule download_clinvar:
    """Download and filter clinvar:
    - remove indels
    - keep only chromosome 22
    - keep only variants where alt != '.'
    - replace 22 -> chr22
    - keep only Pathogenic variants
    """
    output:
        vcf = "input/clinvar_{}.pathogenic.chr22.vcf.gz".format(CLINVAR_DATE)
    shell:
        """
        wget -O - ftp://ftp.ncbi.nlm.nih.gov/pub/clinvar/vcf_GRCh37/archive_2.0/2018/clinvar_{CLINVAR_DATE}.vcf.gz | \
          zcat | vcftools --vcf - --remove-indels --recode --recode-INFO-all -c --chr 22 | \
          awk '$5 != "."' | \
          sed  -e 's/^22/chr22/' | \
          grep -E '^#|CLNSIG=Pathogenic' | \
          gzip -c > {output.vcf}
        """
